import "pim_messages.thingml"
import "timestuff.thingml"
import "javatimer.thingml"

/*
 * This is the digital Shadow of Oystein's Room based on some data from his room.
 * To increase the speed of the shadow, decrease the property called time_interval_ms
 */
thing RoomShadow includes OnOffMsg, TemperatureMsg, TimeStuff, TimerMsgs
	{
	provided port switch_control {
		
	}
	
	required port shadow_mqtt {
		sends temperature
		receives SwitchOn, SwitchOff
	}
	
	required port timer_port{
		receives timer_timeout
		sends timer_start
	}
	
	property current_temp:Float = 20.0
	property switch_on_state:Boolean = true 
//	property time_multiplier:Integer = 1
	property time_interval_ms:Integer = 10*1000 //  10 s

	statechart RoomSimulation init Setup{
		state Setup{
			on entry do
				timer_port!timer_start(time_interval_ms)
			end
			transition -> SwitchStateOn
			guard switch_on_state == true
			
			transition -> SwitchStateOff
			guard switch_on_state == false
		} // End state Setup 
		
		state SwitchStateOn{
			transition -> SwitchStateOff
			event swoff:shadow_mqtt?SwitchOff
			
			transition -> SwitchStateOn
			event newtemp:timer_port?timer_timeout
			action do
				current_temp = current_temp+0.5
				println("Switch is On | New temp from shadow room " + current_temp)
				shadow_mqtt!temperature(1,"temperature ",current_temp)	
				timer_port!timer_start(time_interval_ms)
			end
		} //End state SwitchOn
		
		state SwitchStateOff{
			transition -> SwitchStateOn
			event swon:shadow_mqtt?SwitchOn
			
			transition -> SwitchStateOff
			event newtemp:timer_port?timer_timeout
			action do
				current_temp = current_temp-0.5
				println("Switch is Off | New temp from shadow room " + current_temp)
				shadow_mqtt!temperature(1,"temperature ",current_temp)
				timer_port!timer_start(time_interval_ms)
			end
		} //End state SwitchOff
	} // End statechart
} // End thing