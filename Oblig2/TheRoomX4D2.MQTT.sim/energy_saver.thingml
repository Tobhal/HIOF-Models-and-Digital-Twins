import "datatypes.thingml" from stl
import "pim_messages.thingml"
import "timestuff.thingml"
import "javatimer.thingml"

thing EnergySaver includes TemperatureMsg, LuminanceMsg, GeneralMsg, TimeStuff, TimerMsgs {
     // Ports.
    required port send_es_cmd {
        sends set_temperature  // pass on to pim	 
    }
    
    provided port get_es_cmd {
        receives set_temperature, // to receive as input
        		luminance,
        		set_lum_threshold	
	}
	
	required port timer_port {
		sends timer_start					// Timer starts
	}
	
	provided port timer_port_es {
			receives timer_timeout				// Timer expires
	}
	
	required port request_sensor {
		sends add_lightsensor
	}
	
	// Properties.
    property comfort_temp:Double           // Store comfort temperature
    property lights_off:Double = 2 // Store temperature offset for when lights are off
    property night_temp_delta:Double = 8           // Store temperature offset for when there is night

    property last_lum: Double = 20 // Last recored luminance value. Defaults to 20

    property lum_threshold: Double = 30  // Threshold for when the room is dark or not. Defaults to 30
	property isnight:Boolean // True if it is night
	property light_sens_id:Integer = 2 // The single light sensor in the system

    statechart ES_behavior init Setup {
        on entry do
        	request_sensor!add_lightsensor(light_sens_id)
            println("ES: Running Energy Saver")
            isnight = Night(Now())	//Check if it's night and no one present
			var time_left:Long = 0
			if (isnight) do
				println("ES: It's night time")
				time_left = LeftOfNight()
			end
			else do
				println("ES: It's day time")
				time_left = LeftOfDay()
			end
			timer_port!timer_start(time_left)
			println("ES: Started day/night timer with " + time_left/1000 + "s until it changes again.")
			
			send_es_cmd!set_temperature(20)	// just to simplify testing
        end
        
        state Setup{
        	// Setting initial state based on time and luminance
          	transition -> Present
          	event set_temp:get_es_cmd?set_temperature
          	guard last_lum>=lum_threshold
          	action do
          		comfort_temp = set_temp.t
          		send_es_cmd!set_temperature(set_temp.t)
				println("ES: Set temp = " + set_temp.t + ". Setup -> Present")
          	end
          	
          	transition -> Night
          	event set_temp:get_es_cmd?set_temperature
          	guard isnight
          	action do
          		comfort_temp = set_temp.t
          		send_es_cmd!set_temperature(set_temp.t)
				println("ES: Set temp = " + set_temp.t + ". Setup -> Night")
          	end

          	transition -> Day
          	event set_temp:get_es_cmd?set_temperature
          	guard not isnight
          	action do
          		comfort_temp = set_temp.t
          		send_es_cmd!set_temperature(set_temp.t)
				println("ES: Set temp = " + set_temp.t + ". Setup -> Day")
          	end
	
	        internal event fetch:get_es_cmd?set_lum_threshold
			action do
				lum_threshold = fetch.lum
			end
			
			internal event fetch:get_es_cmd?luminance
			action do
				last_lum = fetch.lum
			end
		}// end Setup
		
		state Present {
			on entry do
				println("ES: Entering Present State")
			end
			// When we receive a new luminance value, check if it's lower than threshold
			
			// Night time and no one present
			transition -> Night
			event recieve_lum: get_es_cmd?luminance
			guard isnight and (recieve_lum.lum<=lum_threshold)
			action do
				send_es_cmd!set_temperature(comfort_temp - night_temp_delta) // night time and people not present
				println("ES: People are not present anymore")
				println("ES: New temp is " + (comfort_temp - night_temp_delta) + " for night time")
			end
				
			// Day time, and no one present
			transition -> Day
			event recieve_lum: get_es_cmd?luminance
			guard not isnight and (recieve_lum.lum<=lum_threshold)
			action do
				send_es_cmd!set_temperature(comfort_temp - lights_off) // day time and people not present
				println("ES: People are not present anymore")
				println("ES: New temp is " + (comfort_temp - lights_off) + " for day time")
			end

			// Internal events
			// Get values
		
			internal event get_temp:get_es_cmd?set_temperature
	        action do
	           	comfort_temp = get_temp.t
	           	send_es_cmd!set_temperature(comfort_temp)
				println("ES: Get temp = " + get_temp.t)
	        end
	
	        internal event fetch:get_es_cmd?set_lum_threshold
			action do
				lum_threshold = fetch.lum
			end
        } // end Present
        	
        state Day {
			on entry do
				println("ES: Entering Day State")
			end
	        	
	        // Transitions to other states within the state
			transition -> Present
			event recieve_lum: get_es_cmd?luminance
			guard recieve_lum.lum>lum_threshold
			action do
				send_es_cmd!set_temperature(comfort_temp) //
				println("ES: Updated to comfort temp " + (comfort_temp) + " for day time")
			end
				
			transition -> Night
			event day_night_change:timer_port_es?timer_timeout
			action do
				// isnight swithes
				isnight = Night(Now())
				
				// Reset timer 
				var time_left:Long = LeftOfNight()
				timer_port!timer_start(time_left)
				
				// Change comfort temperature
				send_es_cmd!set_temperature(comfort_temp - night_temp_delta) //
				println("ES: New temp is " + (comfort_temp - night_temp_delta) + " for night time")
			end
				
			internal event get_temp:get_es_cmd?set_temperature
			action do
				comfort_temp = get_temp.t
				send_es_cmd!set_temperature(comfort_temp)
				println("ES: Get temp = " + get_temp.t)
			end
        } // end Day
        	
        state Night{
			on entry do
				println("ES: Entering Night State")
			end
			// Transitions to other states within the state
				
			transition -> Day
			event day_night_change:timer_port_es?timer_timeout
			action do
				// isnight swithes
				isnight = Night(Now())
				
				// Reset timer 
				var time_left:Long = LeftOfDay()
				timer_port!timer_start(time_left)
				
				// Change comfort temperature
				send_es_cmd!set_temperature(comfort_temp-lights_off)
				println("ES: New temp is " + (comfort_temp - lights_off) + " for day time")
			end
				
			// If people present, temp is set to comfort temperature
			transition -> Present
			event recieve_lum:get_es_cmd?luminance
			guard recieve_lum.lum>lum_threshold
			action do
				send_es_cmd!set_temperature(comfort_temp) // if the office is not dark
				println("ES: Updated to comfort temp " + (comfort_temp) + " for night time")
			end
				
			// Internal events
				
			internal event get_temp:get_es_cmd?set_temperature
	        action do
	            comfort_temp = get_temp.t
				send_es_cmd!set_temperature(comfort_temp)
				println("ES: Get temp = " + get_temp.t)
	        end
	           
	        internal event fetch:get_es_cmd?set_lum_threshold
			action do
				lum_threshold = fetch.lum
			end
        } // end Night
    }// End statechart
} // End thing
